// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.5.31'
    ext.compose_version = '1.1.0-beta03'
    ext.coroutines_version = '1.5.2'
    ext.accompanist_version = '0.21.3-beta'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.0.0'
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    plugins.withId("kotlin-kapt") {
        kapt.useBuildCache = true
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.43.0")
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
}